[[naming:Default]]
[role=group,includesConstraints="naming:EntitiesInModelPackages,naming:TestClassName"]

=== Namensregeln

Die folgende Regeln sind für Namen von Klassen und Packages definiert:

- <<naming:TestClassName>>
- <<naming:EntitiesInModelPackages>>
- <<naming:PackageNameContainsModuleName>>

==== Überprüfung der Testklassennamen

[[naming:TestClassName]]
.Alle JUnit4-Testklassennamen müssen mit `Test` enden.
[source,cypher,role=constraint,requiresConcepts="junit4:TestClass"]
----
MATCH
  (c:Junit4:Test:Class)
WHERE NOT
  c.name =~ ".*Test"
RETURN
  c as InvalidTestClass
----

==== Überprüfung der Klassennamen von Entities

[[naming:EntitiesInModelPackages]]
.Alle JPA Entities müssen in einem Package `entities` sein.
[source,cypher,role=constraint,requiresConcepts="jpa2:Entity"]
----
MATCH
  (package:Package)-[:CONTAINS]->(entity:Jpa:Entity)
WHERE
  package.name <> "entities"
RETURN
  entity as EntityInWrongPackage
----


==== Packages

[[naming:PackageNameContainsModuleName]]
.All types must be located in packages containing the group (groupId) and name (artifactId) of the Maven module.
[source,cypher,role=constraint]
----
MATCH
  (:Maven:Project)-[:CREATES]->(artifact:Artifact)
WITH
  artifact, artifact.group + "\\." + artifact.name + "\\..*" as prefixPattern
MATCH
  (artifact)-[:CONTAINS]->(type:Type)
WHERE NOT
  type.fqn =~ prefixPattern
RETURN
  type as TypeInInvalidPackage
----
